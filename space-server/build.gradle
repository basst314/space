plugins {
	id 'java'
	id 'jacoco'
	id 'application'
	id 'com.github.kt3k.coveralls' version '2.6.3'
}


group 'com.space.server'
version '1.0.0-SNAPSHOT'

sourceCompatibility = 1.8

mainClassName = "com.space.server.web.SpaceStarterWeb"

repositories {
	jcenter()
}

dependencies {
	compile project(':space-common')

	compile 'com.sparkjava:spark-core:2.5'
	compile 'com.google.code.gson:gson:2.7'

	// Spring dependencies
	compile 'org.springframework:spring-core:4.3.5.RELEASE'
	compile 'org.springframework:spring-context:4.3.5.RELEASE'
	compile 'org.springframework:spring-jdbc:4.3.5.RELEASE'

	// Database + Connection Pool
	compile 'org.hsqldb:hsqldb:2.3.3'
	compile 'org.apache.commons:commons-dbcp2:2.1.1'


	testCompile 'junit:junit:4.12'
	testCompile 'org.assertj:assertj-core:3.5.2'
	testCompile 'org.mockito:mockito-core:2.2.5'
}

jacocoTestReport {
	reports {
		xml.enabled = true
		html.enabled = true
	}
}

task fatJar(type: Jar) {
	manifest.from jar.manifest
   classifier = 'all'

	manifest {
		attributes(
			'Class-Path': configurations.compile.collect { it.getName() }.join(' '),
			'Main-Class': 'com.space.server.web.SpaceStarterWeb'
		)
	}

	from { configurations.compile.collect { it.isDirectory() ? it : zipTree(it) } } {
		exclude '*.html'
	}

	// include space-client in this jar
	from ('../space-client/dist') {
		into ('static')
	}

	with jar
}

build.finalizedBy(fatJar)
